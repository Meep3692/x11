plugins {
    id 'java-library'
    id 'io.freefair.lombok' version '5.0.1'
    id 'org.unbroken-dome.test-sets' version '3.0.1'
    id 'org.ajoberstar.reckon' version '0.13.0'
    id 'org.sonarqube' version '3.0'
    id 'com.github.moaxcp.x11protocol'
    id 'java-test-fixtures'
    id 'maven-publish'
    id 'de.marcphilipp.nexus-publish' version '0.4.0'
    id 'signing'
    id 'jacoco'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
    withJavadocJar()
}

group = 'com.github.moaxcp.x11'
description = 'An x11 client implemented in java'

reckon {
    scopeFromProp()
    snapshotFromProp()
}

repositories {
    jcenter()
}

dependencies {
    implementation 'com.kohlschutter.junixsocket:junixsocket-core:2.3.2'
    testImplementation 'org.assertj:assertj-core:3.16.0'
    testImplementation 'org.mockito:mockito-core:3.3.3'
    testCompile 'org.mockito:mockito-junit-jupiter:3.3.3'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
}

testSets {
    integrationTest
}

test {
    useJUnitPlatform()
}

integrationTest {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

x11Protocol {
    outputSrc = file('build/generated/xproto/java/')
    outputResources = file('build/generated/xproto/resources')
    xcbXmls = file('src/main/xcbXmls')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.github.moaxcp.x11'
            artifactId = project.name
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/moaxcp/x11-client'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'http://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'moaxcp'
                        name = 'John Mercier'
                        email = 'moaxcp@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/moaxcp/x11-client.git'
                    developerConnection = 'scm:git:git@github.com:moaxcp/x11-client.git'
                    url = 'https://github.com/moaxcp/x11-client'
                }
            }
            repositories {
                maven {
                    name = "OSS"
                    def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
                    def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
                    url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                    credentials {
                        username = project.findProperty('nexus.username')
                        password = project.findProperty('nexus.password')
                    }
                }
                maven {
                    name = "GitHubPackages"
                    url = uri("https://maven.pkg.github.com/moaxcp/x11-client")
                    credentials {
                        username = project.findProperty('github.user')
                        password = project.findProperty('github.key')
                    }
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = project.findProperty('nexus.username')
            password = project.findProperty('nexus.password')
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

sonarqube {
    properties {
        property "sonar.projectKey", "com.github.moaxcp.x11:x11-client"
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'moaxcp'
    }
}